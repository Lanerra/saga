# Initialization Grammars
# Includes: Global Outline, Character List, Character Sheet, Chapter Outline

root ::= global_outline | character_list | character_sheet | chapter_outline

# 1. Global Outline
global_outline ::= "{" ws "\"act_count\"" ws ":" ws json_number "," ws "\"acts\"" ws ":" ws act_list "," ws "\"inciting_incident\"" ws ":" ws json_string "," ws "\"midpoint\"" ws ":" ws json_string "," ws "\"climax\"" ws ":" ws json_string "," ws "\"resolution\"" ws ":" ws json_string "," ws "\"character_arcs\"" ws ":" ws char_arc_list "," ws "\"thematic_progression\"" ws ":" ws json_string "," ws "\"pacing_notes\"" ws ":" ws json_string ws "}"

act_list ::= "[" ws (act_object ("," ws act_object)*)? ws "]"
act_object ::= "{" ws "\"number\"" ws ":" ws json_number "," ws "\"description\"" ws ":" ws json_string "," ws "\"chapters\"" ws ":" ws json_number ws "}"

char_arc_list ::= "[" ws (char_arc_object ("," ws char_arc_object)*)? ws "]"
char_arc_object ::= "{" ws "\"name\"" ws ":" ws json_string "," ws "\"arc\"" ws ":" ws json_string ws "}"

# 2. Character List
character_list ::= string_list

# 3. Character Sheet (CharacterProfile)
character_sheet ::= "{" ws "\"name\"" ws ":" ws json_string "," ws "\"description\"" ws ":" ws json_string "," ws "\"traits\"" ws ":" ws string_list "," ws "\"status\"" ws ":" ws json_string "," ws "\"motivations\"" ws ":" ws json_string "," ws "\"background\"" ws ":" ws json_string "," ws "\"skills\"" ws ":" ws string_list "," ws "\"relationships\"" ws ":" ws relationship_map "," ws "\"internal_conflict\"" ws ":" ws json_string ws "}"

relationship_map ::= "{" ws (relationship_entry ("," ws relationship_entry)*)? ws "}"
relationship_entry ::= json_string ws ":" ws json_string

# 4. Chapter Outline
chapter_outline ::= "{" ws "\"scene_description\"" ws ":" ws json_string "," ws "\"key_beats\"" ws ":" ws string_list "," ws "\"plot_point\"" ws ":" ws json_string ws "}"
