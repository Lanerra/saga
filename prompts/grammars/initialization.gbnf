# Initialization Grammars
# Includes: Global Outline, Character List, Character Sheet, Chapter Outline,
# plus init-time extraction helpers (structured character fields + world items).

root ::= global-outline | character-list | character-sheet | chapter-outline | structured-character-extraction | world-items-extraction

# 1. Global Outline
global-outline ::= "{" ws "\"act_count\"" ws ":" ws json-number "," ws "\"acts\"" ws ":" ws act-list "," ws "\"inciting_incident\"" ws ":" ws json-string "," ws "\"midpoint\"" ws ":" ws json-string "," ws "\"climax\"" ws ":" ws json-string "," ws "\"resolution\"" ws ":" ws json-string "," ws "\"character_arcs\"" ws ":" ws char-arc-list "," ws "\"thematic_progression\"" ws ":" ws json-string "," ws "\"pacing_notes\"" ws ":" ws json-string ws "}"

act-list ::= "[" ws (act-object ("," ws act-object)*)? ws "]"
act-object ::= "{" ws "\"act_number\"" ws ":" ws json-number "," ws "\"title\"" ws ":" ws json-string "," ws "\"summary\"" ws ":" ws json-string "," ws "\"key_events\"" ws ":" ws string-list "," ws "\"chapters_start\"" ws ":" ws json-number "," ws "\"chapters_end\"" ws ":" ws json-number ws "}"

char-arc-list ::= "[" ws (char-arc-object ("," ws char-arc-object)*)? ws "]"
char-arc-object ::= "{" ws "\"character_name\"" ws ":" ws json-string "," ws "\"starting_state\"" ws ":" ws json-string "," ws "\"ending_state\"" ws ":" ws json-string "," ws "\"key_moments\"" ws ":" ws string-list ws "}"

# 2. Character List
character-list ::= string-list

# 3. Character Sheet (CharacterProfile)
# Bounded strings to prevent runaway generations that get truncated mid-JSON-string.
character-sheet ::= "{" ws "\"name\"" ws ":" ws character-name-string "," ws "\"description\"" ws ":" ws character-description-string "," ws "\"traits\"" ws ":" ws trait-list "," ws "\"status\"" ws ":" ws "\"Active\"" "," ws "\"motivations\"" ws ":" ws character-short-string "," ws "\"background\"" ws ":" ws character-short-string "," ws "\"skills\"" ws ":" ws character-skill-list "," ws "\"relationships\"" ws ":" ws relationship-map "," ws "\"internal_conflict\"" ws ":" ws character-short-string ws "}"

character-name-string ::= "\"" ( [^"\\\n\r] | "\\" (["\\/bfnrt] | "u" [0-9a-fA-F]{4}) ){1,80} "\""
character-description-string ::= "\"" ( [^"\\\n\r] | "\\" (["\\/bfnrt] | "u" [0-9a-fA-F]{4}) ){1,1200} "\""
character-short-string ::= "\"" ( [^"\\\n\r] | "\\" (["\\/bfnrt] | "u" [0-9a-fA-F]{4}) ){0,320} "\""
character-skill-string ::= "\"" ( [^"\\\n\r] | "\\" (["\\/bfnrt] | "u" [0-9a-fA-F]{4}) ){1,40} "\""
character-skill-list ::= "[" ws (character-skill-string ("," ws character-skill-string)*)? ws "]"

character-relationship-string ::= "\"" ( [^"\\\n\r] | "\\" (["\\/bfnrt] | "u" [0-9a-fA-F]{4}) ){1,220} "\""
relationship-map ::= "{" ws (relationship-entry ("," ws relationship-entry)*)? ws "}"
relationship-entry ::= character-name-string ws ":" ws character-relationship-string

# 4. Chapter Outline
chapter-outline ::= "{" ws "\"scene_description\"" ws ":" ws json-string "," ws "\"key_beats\"" ws ":" ws string-list "," ws "\"plot_point\"" ws ":" ws json-string ws "}"

# 5. Structured Character Extraction (init-time, derived from character sheet prose)
# Root shape:
# {"traits":[...],"status":"...","motivations":"...","background":"..."}
structured-character-extraction ::= "{" ws "\"traits\"" ws ":" ws trait-list "," ws "\"status\"" ws ":" ws json-string "," ws "\"motivations\"" ws ":" ws json-string "," ws "\"background\"" ws ":" ws json-string ws "}"

# 6. World Items Extraction (init-time, derived from global outline prose)
# Root shape:
# [{"name":"...","category":"location|object","description":"..."}, ...]
world-items-extraction ::= "[" ws (world-item-object ("," ws world-item-object)*)? ws "]"
world-item-object ::= "{" ws "\"name\"" ws ":" ws json-string "," ws "\"category\"" ws ":" ws world-item-category "," ws "\"description\"" ws ":" ws json-string ws "}"
world-item-category ::= "\"location\"" | "\"object\""
