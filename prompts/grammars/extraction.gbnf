# Extraction Grammars
# Includes: Character, Location (World), Event, Relationship Extraction

root ::= character-extraction | relationship-extraction | world-extraction | location-extraction | event-extraction

# 1. Character Extraction
character-extraction ::= "{" ws "\"character_updates\"" ws ":" ws character-map ws "}"
character-map ::= "{" ws (character-entry ("," ws character-entry)*)? ws "}"
character-entry ::= json-string ws ":" ws character-details
character-details ::= "{" ws "\"description\"" ws ":" ws json-string "," ws "\"traits\"" ws ":" ws trait-list "," ws "\"status\"" ws ":" ws json-string "," ws "\"relationships\"" ws ":" ws relationship-map ws "}"

# 2. World Extraction (generic, allows arbitrary bucket keys)
world-extraction ::= "{" ws "\"world_updates\"" ws ":" ws category-map ws "}"
category-map ::= "{" ws (category-entry ("," ws category-entry)*)? ws "}"
category-entry ::= json-string ws ":" ws entity-map

# 2a. World Extraction (strict roots for known buckets)
location-extraction ::= "{" ws "\"world_updates\"" ws ":" ws "{" ws "\"Location\"" ws ":" ws entity-map ws "}" ws "}"
event-extraction ::= "{" ws "\"world_updates\"" ws ":" ws "{" ws "\"Event\"" ws ":" ws entity-map ws "}" ws "}"

entity-map ::= "{" ws (entity-entry ("," ws entity-entry)*)? ws "}"
entity-entry ::= json-string ws ":" ws entity-details

# Entity schema (category is mandatory)
# Subtype semantics live in "category" while the canonical label/bucket remains Location/Event/Item/Organization/Concept.
entity-details ::= "{" ws "\"description\"" ws ":" ws json-string "," ws "\"category\"" ws ":" ws json-string "," ws "\"goals\"" ws ":" ws string-list "," ws "\"rules\"" ws ":" ws string-list "," ws "\"key_elements\"" ws ":" ws string-list ws "}"

# 3. Relationship Extraction (Triples)
relationship-extraction ::= "{" ws "\"kg_triples\"" ws ":" ws triple-list ws "}"
triple-list ::= "[" ws (triple-object ("," ws triple-object)*)? ws "]"
triple-object ::= "{" ws "\"subject\"" ws ":" ws json-string "," ws "\"predicate\"" ws ":" ws json-string "," ws "\"object_entity\"" ws ":" ws json-string "," ws "\"description\"" ws ":" ws json-string ws "}"

# --- Common Definitions ---
relationship-map ::= "{" ws (relationship-entry ("," ws relationship-entry)*)? ws "}"
relationship-entry ::= json-string ws ":" ws json-string

json-value ::= json-object | json-array | json-string | json-number | json-bool | json-null
json-object ::= "{" ws (json-member ("," ws json-member)*)? ws "}"
json-member ::= json-string ws ":" ws json-value
json-array ::= "[" ws (json-value ("," ws json-value)*)? ws "]"
json-string ::= "\"" ( [^"\\] | "\\" (["\\/bfnrt] | "u" [0-9a-fA-F]{4}) )* "\""
json-number ::= ("-"? ([0-9] | [1-9] [0-9]*)) ("." [0-9]+)? ([eE] [-+]? [0-9]+)?
json-bool ::= "true" | "false"
json-null ::= "null"
ws ::= [ \t\n\r]*
string-list ::= "[" ws (json-string ("," ws json-string)*)? ws "]"

# Trait-specific definitions
# Single-word trait: alphanumeric only, no spaces or special characters (except hyphens for compound traits)
single-word-trait ::= "\"" [a-zA-Z][a-zA-Z0-9-]* "\""
trait-list ::= "[" ws (single-word-trait ("," ws single-word-trait)*)? ws "]"